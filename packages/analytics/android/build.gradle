buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.0'
  }
}

plugins {
  id "io.invertase.gradle.build" version "1.3"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

project.ext {
  set('react-native', [
    versions: [
      android           : [
        minSdk    : 16,
        targetSdk : 28,
        compileSdk: 28,
        // optional as gradle.buildTools comes with one by default
        // overriding here though to match the version RN uses
        buildTools: "28.0.3"
      ],

      googlePlayServices: [
        base: "16.1.0",
      ],

      firebase          : [
        analytics: "16.4.0"
      ],
    ],
  ])
}

android {
  defaultConfig {
    multiDexEnabled true
  }
  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  jcenter()
}

dependencies {
  implementation "com.google.firebase:firebase-analytics:${ReactNative.ext.getVersion("firebase", "analytics")}"
  implementation "com.google.android.gms:play-services-base:${ReactNative.ext.getVersion("googlePlayServices", "base")}"
  implementation 'androidx.annotation:annotation:1.0.2'
  implementation "androidx.core:core-ktx:+"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
//ReactNative.module.applyReactNativeDependency("api")

// Flutter
if (new File(rootProject.projectDir.parentFile, 'pubspec.yaml').exists()) {
  apply from: file("./flutter.gradle")
// React Native
} else if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: file("./react.gradle")
}
